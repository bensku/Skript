test "replace items":
	set {_inv} to chest inventory with 3 rows
	set slot 2 of {_inv} to 54 dirt
	set slot 3 of {_inv} to 30 bone
	set slot 5 of {_inv} to (45 dirt) named "test"
	set slot 6 of {_inv} to 20 iron ingot
	replace all dirt in {_inv} with bone
	set {_n::*} to 54, 30 and 45
	loop all items in inventory {_inv}:
		assert loop-item is not dirt with "replace was incomplete"
		if loop-item is bone:
			increase {_i} by 1
			assert item amount of loop-item is {_n::%{_i}%} with "replacement item amount not preserved %{_i}%"
	set {_inv2} to {_inv}
	replace all emerald in {_inv} with diamond
	assert {_inv} is {_inv2} with "replace non-existent item changed inventory"


test "replace strings":
	loop 16 times:
		set {_text::%loop-number%} to "My name is bob and this is my friend bob! Say hi, bob!"

	# Original Replace
	replace all "bob" in {_text::1} with "bobby"
	replace all "bob" with "bobby" in {_text::2}

	# Replace First
	replace first "bob" in {_text::3} with "bobby"
	replace first "bob" with "bobby" in {_text::4}

	#Replace Last
	replace last "bob" with "bobby" in {_text::5}
	replace last "bob" in {_text::6} with "bobby"


	# Case Sensitivity
	replace all "bob" in {_text::9} with "BOBBY" with case sensitivity
	replace all "BOB" with "bobby" in {_text::10} with case sensitivity
	replace first "bob" in {_text::11} with "BOBBY" with case sensitivity
	replace first "BOB" with "bobby" in {_text::12} with case sensitivity
	replace last "bob" in {_text::13} with "BOBBY" with case sensitivity
	replace last "BOB" with "bobby" in {_text::14} with case sensitivity

	assert {_text::1} is "My name is bobby and this is my friend bobby! Say hi, bobby!" with "replace all pattern 1 (case sensitivity disabled) (%{_text::1}%) failed"
	assert {_text::2} is "My name is bobby and this is my friend bobby! Say hi, bobby!" with "replace all pattern 2 (case sensitivity disabled) (%{_text::2}%) failed"
	assert {_text::3} is "My name is bobby and this is my friend bob! Say hi, bob!" with "replace first pattern 1 (case sensitivity disabled) (%{_text::3}%) failed"
	assert {_text::4} is "My name is bobby and this is my friend bob! Say hi, bob!" with "replace first pattern 2 (case sensitivity disabled) (%{_text::4}%) failed"
	assert {_text::5} is "My name is bob and this is my friend bob! Say hi, bobby!" with "replace last pattern 1 (case sensitivity disabled) (%{_text::6}%) failed"
	assert {_text::6} is "My name is bob and this is my friend bob! Say hi, bobby!" with "replace last pattern 2 (case sensitivity disabled) (%{_text::5}%) failed"
	assert {_text::9} is "My name is BOBBY and this is my friend BOBBY! Say hi, BOBBY!" with "replace all pattern 1 (case sensitivity enabled) (%{_text::9}%) failed"
	assert {_text::10} is "My name is bob and this is my friend bob! Say hi, bob!" with "replace all pattern 2 (case sensitivity enabled) (%{_text::10}%) failed"
	assert {_text::11} is "My name is BOBBY and this is my friend bob! Say hi, bob!" with "replace first pattern 1 (case sensitivity enabled) (%{_text::11}%) failed"
	assert {_text::12} is "My name is bob and this is my friend bob! Say hi, bob!" with "replace first pattern 2 (case sensitivity enabled) (%{_text::12}%) failed"
	assert {_text::13} is "My name is bob and this is my friend bob! Say hi, BOBBY!" with "replace last pattern 1 (case sensitivity enabled) (%{_text::13}%) failed"
	assert {_text::14} is "My name is bob and this is my friend bob! Say hi, bob!" with "replace last pattern 2 (case sensitivity enabled) (%{_text::14}%) failed"
